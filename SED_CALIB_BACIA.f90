
SUBROUTINE SED_CALIB_BACIA

USE SED_VARS
USE VARS_MAIN
USE VARS_CALIB
IMPLICIT NONE

!@ RETARDOS DO ESCOAMENTO SUPERFICIAL (s)
REAL TKSed
!@ LAMINAS DO ESCOAMENTO SUPERFICIAL (mm)
REAL DSUP

REAL(4) FRAC(NU)
INTEGER iUSO,i

PSC = 0.0


!VERIFICA A DATA CORRESPONDE O DIA JULIANO
CALL CALDAT(IDINI + IT - 1,IMES,IDIA,IANO)

DO IC=1,NC	!@ LOOP DAS MINIBACIAS
	
	IF (sbtFLAG(iC)==1) CYCLE !@ Nao computa minibacias a montante de minibacias com substituicao de dados
	!@ Neste caso, para sedimentos, ainda terá que ser fornecida uma concentração de sedimentos

    IB=IBAC(IC)
    
	IF(IB>SUBfim.OR.IB<SUBini)CYCLE

	DO IU=1,NU	!@ LOOP DOS USOS

		APIX = 0.0
		FCTE = 0.0
		QPIC = 0.0

		IF (NPXU(IC,IU) .LE. 0.0) THEN  !@ NAO TEM PIXEL(S) COM ESTE USO NESTA MINIBACIA
			CYCLE !@ PASSA PARA O PROXIMO USO
		ENDIF

		IF(PUSO(IC,IU).LT.0.0001)THEN !@ NAO TEM ESTE USO NESTA MINIBACIA
			CYCLE !@ PASSA PARA O PROXIMO USO
		ENDIF
		
		DSUP = DSUP_SC(IC,IT,IU)
        
		!@ ÁREA MÉDIA DOS PIXELS DE CADA USO DA MINIBACIA (KM^2)
		APIX = (ACEL(IC)*(PUSO(IC,IU)/100.))/NPXU(IC,IU) !@ (KM^2)
		APIX = APIX*100. !@ (Ha = hectares)


		!@ FATOR CONSTANTE DA MUSLE PARA CADA USO DA MINIBACIA
		FCTE = ALFsed(IB)*(APIX**BETsed(IB))*Kusle(IB,IU)*Cusle(IB,IU)*Pusle(IB,IU)*Rgros(IB,IU)*LSAcu(IC,IU)   ! HUGO FAGUNDES 04/10/17
		APIX = APIX/100. !@ (KM^2)


		IF (DSUP < 0.0) DSUP = 0.0 !@ CORREÇÃO DE DSUP NEGATIVO VINDO DA ROTINA "CELULA"

		!@ TAXA DE PICO DO ESCOAMENTO SUPERFICIAL PELA MÉTODO RACIONAL, CONSIDERANDO
        !@ A INTENSIDADE DA PRECIPITAÇÃO DENTRO DAS 24 HORAS (DIA) (M^3/S)
        IF (P(IC) > 0.0) QPIC = ((DSUP/P(IC))*(P(IC)/24.)*APIX)/3.6 !@ (M^3/S)
        SLU(IC,IU) = SLU(IC,IU) + FCTE*(DSUP**BETsed(IB))*(QPIC**BETsed(IB)) !@ (TON)   HUGO FAGUNDES 04/10/17       
	ENDDO	! FIM DO LOOP DOS USOS

    FRAC = 0.0  !@ porcentagem de sedimento de cada bloco da minibacia
    if (sum(SLU(IC,:)) /= 0.0) then
        FRAC(:) = SLU(IC,:)/sum(SLU(IC,:))
    endif


	!@ PARÂMETRO DE RETARDO DO ESCOAMENTO SUPERFICIAL
	TKSed = AuxTKS(IB)*TKS_SC(IC)! HUGO FAGUNDES 11/07/19

	!@ DESCARGA SÓLIDA DAS MINIBACIAS (TON/S) NO TEMPO IT
	!QSC(IC)   = SLC(IC)/TKSed
	QSU(IC,:) = SLU(IC,:)/TKSed	!@ 21/02/2011

	!@ ATUALIZA PERDA DE SOLO DAS MINIBACIAS (TON) NO TEMPO IT
	!SLX     = SLC(IC)   - QSC(IC)*DTP !@ verifica o que sobra de sedimentos na minibacia
	SLXU(:) = SLU(IC,:) - QSU(IC,:)*DTP !@ 21/02/2011

	DO IU=1,NU
		IF(SLXU(IU) < 0.0)THEN				!@ TKSed < DTP
			QSU(IC,IU) = SLU(IC,IU)/(DTP)
			SLU(IC,IU) = 0.0
		ELSE
			SLU(IC,IU) = SLXU(IU)
		ENDIF
	ENDDO

    !@ APORTE DE SEDIMENTOS DAS BLOCOS DAS MINIBACIAS PARA O RIO (TON)
	PSU(IC,:) = QSU(IC,:)*DTP	!@ 21/02/2011

	!@ APORTE DE SEDIMENTOS DAS MINIBACIAS PARA O RIO (TON)
	PSC(IC,1) = sum(QSU(IC,:))*DTP
	DO iUSO=1,NU-1
	    PSC(IC,2) = PSC(IC,2) + PSC(IC,1)*FRAC(iUSO)*Mareia(IB,iUSO)/100	!@ APORTE DE AREIA QUE CHEGA À DRENAGEM
	    PSC(IC,3) = PSC(IC,3) + PSC(IC,1)*FRAC(iUSO)*Msilte(IB,iUSO)/100	!@ APORTE DE SILTE QUE CHEGA À DRENAGEM
	    PSC(IC,4) = PSC(IC,4) + PSC(IC,1)*FRAC(iUSO)*Margila(IB,iUSO)/100	!@ APORTE DE ARGILA QUE CHEGA À DRENAGEM
    ENDDO   
ENDDO	!@ FIM DO LOOP DAS MINIBACIAS

RETURN

END